USE CorporeRM
GO 
/**/




DECLARE @CODCOLIGADA INT = 1
DECLARE @DATAINICIAL DATETIME = '01/01/2020'
DECLARE @DATAFINAL   DATETIME = '05/31/2020'
DECLARE @HISTFECHA   INT =0       
 
;WITH CONTAS
AS 
(
	SELECT CODCOLIGADA, CODCONTA, DESCRICAO 
	FROM DBO.CCONTA 
	WHERE CODCOLIGADA=@CODCOLIGADA
	    /*AND 1=1--ANALITICA = 1 */
)
,TAB_1
AS
(
	SELECT 
		 L.CODCOLIGADA
		,L.CONTA
		,SUM(CASE WHEN TIPO='D' THEN VALOR ELSE 0 END) AS 'DEBITO'
		,SUM(CASE WHEN TIPO='C' THEN VALOR ELSE 0 END) AS 'CREDITO'
	FROM VW_LCTOS_CONTABILIDADE L
	WHERE L.CODCOLIGADA=@CODCOLIGADA
	AND L.[DATA] BETWEEN @DATAINICIAL AND @DATAFINAL
	AND L.HISTFECHA=@HISTFECHA
	GROUP BY L.CODCOLIGADA, L.CONTA, L.TIPO 
)
,TAB_2
AS
(
	SELECT 
		 CODCOLIGADA
		,CONTA
		,SUM(DEBITO) AS 'DEBITO'
		,SUM(CREDITO) AS 'CREDITO'
	  FROM TAB_1
	  GROUP BY CODCOLIGADA, CONTA
)
,TAB_3
AS
(
	SELECT 
		 C.CODCOLIGADA
		,C.CODCONTA
		,ISNULL(SUM(DEBITO),0) AS 'DEBITO'
		,ISNULL(SUM(CREDITO),0) AS 'CREDITO'
	FROM CONTAS C
	LEFT JOIN TAB_2 T ON (C.CODCOLIGADA =T.CODCOLIGADA AND T.CONTA LIKE C.CODCONTA + '%')
	GROUP BY C.CODCOLIGADA, C.CODCONTA
)
,TAB_4
AS
(
SELECT C.CODCOLIGADA, C.CODCONTA,SLDANT
FROM CONTAS C
OUTER APPLY 
(
	SELECT 
		 CODCOLIGADA
		,CONTA
		,SUM(VALOR) AS 'SLDANT'
    FROM VW_LCTOS_CONTABILIDADE 
    WHERE CODCOLIGADA=C.CODCOLIGADA 
    AND CONTA=C.CODCONTA
    AND [DATA]<@DATAINICIAL 
	AND HISTFECHA=@HISTFECHA 
    GROUP BY CODCOLIGADA, CONTA) A
)
, TAB_5
AS
(
	SELECT 
		 C.CODCOLIGADA
		,C.CODCONTA
		,ISNULL(SUM(T.SLDANT),0) AS 'SLDANT'
	FROM CONTAS C
	LEFT JOIN TAB_4 T ON (C.CODCOLIGADA =T.CODCOLIGADA AND T.CODCONTA LIKE C.CODCONTA + '%')
	GROUP BY C.CODCOLIGADA, C.CODCONTA
)
,BALANCETE
AS
(
	SELECT 
		 C.CODCOLIGADA
		,C.CODCONTA
		,C.DESCRICAO
		,A.SLDANT
		,VD.DEBITO
		,VC.CREDITO
		,(VD.DEBITO + VC.CREDITO) AS 'MOVIMENTO'
		,(A.SLDANT + VD.DEBITO + VC.CREDITO) AS 'SALDO'
	FROM CONTAS C
	LEFT JOIN TAB_3 VD ON (C.CODCOLIGADA = VD.CODCOLIGADA AND C.CODCONTA = VD.CODCONTA)
	LEFT JOIN TAB_3 VC ON (C.CODCOLIGADA = VC.CODCOLIGADA AND C.CODCONTA = VC.CODCONTA)
	LEFT JOIN TAB_5 A  ON (C.CODCOLIGADA = A.CODCOLIGADA  AND C.CODCONTA = A.CODCONTA)
)
SELECT 
	 CODCOLIGADA	
	,CODCONTA	
	,DESCRICAO	
	,SLDANT	
	,DEBITO	
	,CREDITO	
	,MOVIMENTO	
	,SALDO
FROM BALANCETE
ORDER BY CODCOLIGADA, CODCONTA
 
 
 
 
 
