USE CORPORERM
GO 

DECLARE @CODCOLIGADA INT = 1
DECLARE @DATAINICIAL DATETIME = '2022-01-01'
DECLARE @DATAFINAL   DATETIME = '2022-07-31'
DECLARE @HISTFECHA   INT = 0       
DECLARE @CCINICIAL   VARCHAR(25) = '1400.0000'
DECLARE @CCFINAL     VARCHAR(25) = '1401.9999'
DECLARE @CCPOSQUEBRA INT = 4

DECLARE @TAB_1 TABLE 
(
	 CODCOLIGADA INT 
	,CONTA VARCHAR(25)
	,CODCCUSTO VARCHAR(25)
	,DEBITO DECIMAL(15,4)
	,CREDITO DECIMAL(15,4)
)

INSERT INTO @TAB_1
SELECT 
	 L.CODCOLIGADA
	,L.CONTA
	,SUBSTRING(L.CODCCUSTO,1,@CCPOSQUEBRA) AS 'CODCCUSTO'
	,SUM(CASE WHEN TIPO='D' THEN VALOR ELSE 0 END) AS 'DEBITO'
	,SUM(CASE WHEN TIPO='C' THEN VALOR ELSE 0 END) AS 'CREDITO'
FROM DBO.VW_LCTOS_CONTABILIDADE L
WHERE L.CODCOLIGADA=@CODCOLIGADA
AND L.[DATA] BETWEEN @DATAINICIAL AND @DATAFINAL
AND L.HISTFECHA = @HISTFECHA
AND L.CODCCUSTO BETWEEN @CCINICIAL AND @CCFINAL
GROUP BY L.CODCOLIGADA, L.CONTA, L.TIPO , SUBSTRING(L.CODCCUSTO,1,@CCPOSQUEBRA)

DECLARE @TAB_1_OLD TABLE 
(
	 CODCOLIGADA INT 
	,CONTA VARCHAR(25)
	,CODCCUSTO VARCHAR(25)
	,VALOR DECIMAL(15,4)
)

INSERT INTO @TAB_1_OLD
SELECT 
	 L.CODCOLIGADA
	,L.CONTA
	,SUBSTRING(L.CODCCUSTO,1,4) AS 'CODCCUSTO'
	,SUM(L.VALOR) AS 'VALOR'
FROM DBO.VW_LCTOS_CONTABILIDADE L
WHERE L.CODCOLIGADA=@CODCOLIGADA
AND L.CODCCUSTO BETWEEN @CCINICIAL AND @CCFINAL
AND L.[DATA] < @DATAINICIAL
AND L.HISTFECHA = @HISTFECHA
GROUP BY L.CODCOLIGADA, L.CONTA, L.TIPO , SUBSTRING(L.CODCCUSTO,1,4)

/**********************************************************************/
 
;WITH CONTAS
AS 
(
	SELECT CODCOLIGADA, CODCONTA, DESCRICAO 
	FROM DBO.CCONTA 
	WHERE CODCOLIGADA=@CODCOLIGADA	  
) 
,TAB_2
AS
(
	 SELECT 
		 CODCOLIGADA
		,CONTA
		,CODCCUSTO
		,SUM(DEBITO) AS 'DEBITO'
		,SUM(CREDITO) AS 'CREDITO'
	  FROM @TAB_1
	  GROUP BY CODCOLIGADA, CONTA, CODCCUSTO
) 
, TAB_2_CC_CONTA
AS
(
	SELECT C.CODCOLIGADA, C.CODCONTA, C.DESCRICAO , D.CODCCUSTO
	FROM CONTAS C
	CROSS APPLY 
	(
		SELECT DISTINCT CODCCUSTO FROM @TAB_1 
	) D
)
,TAB_3
AS
(
	SELECT 
		 C.CODCOLIGADA
		,C.CODCONTA
		,C.CODCCUSTO
		,ISNULL(SUM(DEBITO),0) AS 'DEBITO'
		,ISNULL(SUM(CREDITO),0) AS 'CREDITO'
	FROM TAB_2_CC_CONTA C
	LEFT JOIN TAB_2 T ON (C.CODCOLIGADA = T.CODCOLIGADA AND C.CODCCUSTO = T.CODCCUSTO AND T.CONTA LIKE C.CODCONTA + '%')
	GROUP BY C.CODCOLIGADA, C.CODCONTA , C.CODCCUSTO
) 
,TAB_4
AS
(
	SELECT C.CODCOLIGADA, C.CODCONTA , C.CODCCUSTO , ISNULL(SLDANT,0) AS 'SLDANT'
	FROM TAB_2_CC_CONTA C
	OUTER APPLY 
	(
		SELECT SUM(VALOR) AS 'SLDANT'
	    FROM @TAB_1_OLD 
	    WHERE CODCOLIGADA=C.CODCOLIGADA 
	    AND CONTA=C.CODCONTA
		AND CODCCUSTO=C.CODCCUSTO
	) A
) 
, TAB_5
AS
(
	SELECT 
		 C.CODCOLIGADA
		,C.CODCONTA
		,C.CODCCUSTO
		,ISNULL(SUM(T.SLDANT),0) AS 'SLDANT'
	FROM TAB_2_CC_CONTA C
	LEFT JOIN TAB_4 T ON (C.CODCOLIGADA = T.CODCOLIGADA AND C.CODCCUSTO = T.CODCCUSTO AND T.CODCONTA LIKE C.CODCONTA + '%')
	GROUP BY C.CODCOLIGADA, C.CODCONTA, C.CODCCUSTO
) 
,BALANCETE
AS
(
	SELECT 
		 C.CODCOLIGADA
		,C.CODCONTA
		,C.CODCCUSTO
		,C.DESCRICAO
		,A.SLDANT
		,VD.DEBITO
		,VC.CREDITO
		,(VD.DEBITO + VC.CREDITO) AS 'MOVIMENTO'
		,(A.SLDANT + VD.DEBITO + VC.CREDITO) AS 'SALDO'
	FROM TAB_2_CC_CONTA C
	LEFT JOIN TAB_3 VD ON (C.CODCOLIGADA = VD.CODCOLIGADA AND C.CODCCUSTO = VD.CODCCUSTO AND C.CODCONTA = VD.CODCONTA)
	LEFT JOIN TAB_3 VC ON (C.CODCOLIGADA = VC.CODCOLIGADA AND C.CODCCUSTO = VC.CODCCUSTO AND C.CODCONTA = VC.CODCONTA)
	LEFT JOIN TAB_5 A  ON (C.CODCOLIGADA = A.CODCOLIGADA  AND C.CODCCUSTO = A.CODCCUSTO AND C.CODCONTA = A.CODCONTA)
) 
SELECT 
	 CODCOLIGADA	
	,CODCONTA	
	,DESCRICAO	
	,CODCCUSTO
	,SLDANT	
	,DEBITO	
	,CREDITO	
	,MOVIMENTO	
	,SALDO
FROM BALANCETE
ORDER BY CODCOLIGADA, CODCCUSTO, CODCONTA
OPTION (RECOMPILE, MAXDOP 1)

 
 
 

