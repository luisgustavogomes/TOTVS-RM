USE CorporeRM
GO 


CREATE OR ALTER VIEW VW_LCTOS_CONTABILIDADE
AS
SELECT 
        C.CODCOLIGADA
       ,C.IDPARTIDA
       ,C.CODLOTE
       ,C.CODFILIAL
       ,C.CODCCUSTO
       ,C.DOCUMENTO
       ,C.[DATA]
       ,CASE 
             WHEN C.DEBITO = CC.CODCONTA AND CC.TIPO = 'D' THEN 'D' 
             WHEN C.CREDITO = CC.CODCONTA AND CC.TIPO = 'C' THEN 'C' 
        ELSE NULL
       END AS 'TIPO'
       ,CASE 
             WHEN C.DEBITO  = CC.CODCONTA AND CC.TIPO = 'D' THEN C.DEBITO 
             WHEN C.CREDITO = CC.CODCONTA AND CC.TIPO = 'C' THEN C.CREDITO 
        ELSE NULL 
        END AS 'CONTA'
       ,CASE 
             WHEN C.DEBITO  = CC.CODCONTA AND SUBSTRING(CC.CODCONTA,1,1) IN ('1','4') AND CC.TIPO = 'D' THEN C.VALOR  
             WHEN C.CREDITO = CC.CODCONTA AND SUBSTRING(CC.CODCONTA,1,1) IN ('1','4') AND CC.TIPO = 'C' THEN C.VALOR * -1
             WHEN C.DEBITO  = CC.CODCONTA AND SUBSTRING(CC.CODCONTA,1,1) IN ('3','2') AND CC.TIPO = 'D' THEN C.VALOR * -1 
             WHEN C.CREDITO = CC.CODCONTA AND SUBSTRING(CC.CODCONTA,1,1) IN ('3','2') AND CC.TIPO = 'C' THEN C.VALOR  
        ELSE NULL
       END AS 'VALOR'
       ,(ISNULL(H.DESCRICAO,'') + ' ' + C.COMPLEMENTO) AS 'HISTORICO'
       ,ISNULL(H.HISTFECHA, 0) AS 'HISTFECHA'
       ,C.INTEGRAAPLICACAO
       ,C.INTEGRACHAVE
       ,C.TIPOGERACAO
FROM DBO.CPARTIDA C
CROSS APPLY (SELECT CODCONTA, DESCRICAO , 'C' AS TIPO
               FROM DBO.CCONTA (NOLOCK)
              WHERE CODCOLIGADA = C.CODCOLIGADA
                AND CODCONTA = C.CREDITO
 
                    UNION ALL
 
                SELECT CODCONTA, DESCRICAO , 'D' AS TIPO
               FROM DBO.CCONTA (NOLOCK)
              WHERE CODCOLIGADA = C.CODCOLIGADA
                             AND CODCONTA = C.DEBITO 
                  ) CC 
OUTER APPLY (SELECT DESCRICAO, HISTFECHA 
               FROM DBO.CHISTP 
                      WHERE CODCOLIGADA=C.CODCOLIGADA 
                        AND CODHISTP=C.CODHISTP) H
UNION ALL 
 
SELECT 
       CODCOLIGADA
       ,0 AS 'IDPARTIDA'
       ,0 AS 'CODLOTE'
       ,CODFILIAL
       ,CODCCUSTO
       ,'IMPLANTACAO' AS 'DOCUMENTO'
       ,'1900-12-31' AS DATA
       ,'IS' AS 'TIPO'
       ,CODCONTA
       ,CASE 
             WHEN SUBSTRING(CODCONTA,1,1) IN ('1','4') 
             THEN VALOR 
        ELSE VALOR * -1 
        END AS 'VALOR'
       ,'IMPLANTAÇÃO DE SISTEMAS' AS 'HISTORICO'
       ,0 AS 'HISTFECHA'
       ,'C' AS 'INTEGRAAPLICACAO'
       ,NULL AS 'INTEGRACHAVE'
       ,NULL AS 'TIPOGERACAO'
 FROM DBO.CSDANT C 
WHERE CODCOLIGADA = CODCOLIGADA
AND VALOR <> 0


