CREATE OR ALTER VIEW VW_FUNCIONARIO_HISTORICO_SECAO_FUNCAO
AS
WITH TAB_1
AS
(
	SELECT 
		 P.CODCOLIGADA
		,P.CHAPA
		,P.NOME
		,P.DATAADMISSAO
		,P.DATADEMISSAO
		,S.DTMUDANCA AS 'DE'
		,COALESCE ( S2.DTMUDANCA , P.DATADEMISSAO, DATEADD(DD, DATEDIFF(DD, 0,  GETDATE() ), 0))  AS  'ATE'
		,S.CODSECAO
	  FROM DBO.PFUNC P (NOLOCK)
	  JOIN DBO.PFHSTSEC S (NOLOCK) ON (S.CODCOLIGADA=P.CODCOLIGADA AND S.CHAPA=P.CHAPA) 
	  OUTER APPLY 
	  (
		  SELECT MIN(DTMUDANCA) AS 'DTMUDANCA' 
		   FROM DBO.PFHSTSEC (NOLOCK)
		  WHERE CODCOLIGADA=S.CODCOLIGADA 
		    AND CHAPA=S.CHAPA        
		    AND DTMUDANCA>S.DTMUDANCA
	  ) S2
	 WHERE P.CODCOLIGADA=1 
)
, TAB_2
AS
(
	SELECT 
		 T1.CODCOLIGADA
		,T1.CHAPA
		,T1.NOME
		,T1.DATAADMISSAO
		,T1.DATADEMISSAO
		,T1.DE
		,T1.ATE
		,T1.CODSECAO
		,F.DTMUDANCA
		,F.CODFUNCAO 
	FROM TAB_1 T1
	LEFT JOIN DBO.PFHSTFCO F (NOLOCK) ON (F.CODCOLIGADA=T1.CODCOLIGADA AND F.CHAPA=T1.CHAPA AND F.DTMUDANCA BETWEEN T1.DE AND T1.ATE-1 )
)
SELECT 
	 T2.CODCOLIGADA 
	,T2.CHAPA 
	,T2.NOME
	,T2.DATAADMISSAO
	,T2.DATADEMISSAO 
	,T2.DE 
	,(T2.ATE -1) AS 'ATE'
	,T2.CODSECAO
	,COALESCE(T2.CODFUNCAO, F.CODFUNCAO) AS 'CODFUNCAO'
FROM TAB_2 T2
OUTER APPLY 
(
	SELECT CODFUNCAO 
	FROM DBO.PFHSTFCO H (NOLOCK)
	WHERE H.CODCOLIGADA=T2.CODCOLIGADA
	AND H.CHAPA=T2.CHAPA 
	AND H.DTMUDANCA=(SELECT MAX(DTMUDANCA) 
					  FROM DBO.PFHSTFCO (NOLOCK)
				     WHERE CODCOLIGADA=H.CODCOLIGADA 
					   AND CHAPA=H.CHAPA
					   AND DTMUDANCA <= T2.ATE) 
) F
